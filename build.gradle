buildscript {
  repositories {
    mavenCentral()
    gradlePluginPortal()
    mavenLocal()
    google()
    maven { url = 'https://central.sonatype.com/repository/maven-snapshots/' }
  }
  dependencies {
    classpath "com.android.tools.build:gradle:8.9.3"
    classpath "com.badlogicgames.gdx:gdx-tools:$gdxVersion"
  }
}

//导入纹理包
import com.badlogic.gdx.tools.texturepacker.TexturePacker

project.ext.texturePackerConfig  = ["$rootDir/assets-src/images", "$rootDir/assets/atlas", "game"]

tasks.register('texturePacker') {
  doLast {
    def cfg = project.ext.texturePackerConfig
    logger.lifecycle "Calling TexturePacker: $cfg"
    TexturePacker.process(cfg[0] as String, cfg[1], cfg[2])   // ✅整数下标
  }
}

allprojects {
  apply plugin: 'eclipse'
  apply plugin: 'idea'

  // This allows you to "Build and run using IntelliJ IDEA", an option in IDEA's Settings.
  idea {
    module {
      outputDir = file('build/classes/java/main')
      testOutputDir = file('build/classes/java/test')
    }
  }
}

configure(subprojects - project(':android')) {
  apply plugin: 'java-library'
  java.sourceCompatibility = 8

  // From https://lyze.dev/2021/04/29/libGDX-Internal-Assets-List/
  // The article can be helpful when using assets.txt in your project.
  tasks.register('generateAssetList') {
    inputs.dir("${project.rootDir}/assets/")
    // projectFolder/assets
    File assetsFolder = new File("${project.rootDir}/assets/")
    // projectFolder/assets/assets.txt
    File assetsFile = new File(assetsFolder, "assets.txt")
    // delete that file in case we've already created it
    assetsFile.delete()

    // iterate through all files inside that folder
    // convert it to a relative path
    // and append it to the file assets.txt
    fileTree(assetsFolder).collect { assetsFolder.relativePath(it) }.sort().each {
      assetsFile.append(it + "\n")
    }
  }
  processResources.dependsOn 'generateAssetList'

  compileJava {
    options.incremental = true
  }
}

subprojects {
  version = "$projectVersion"
  ext.appName = 'FirstDemo'
  repositories {
    mavenCentral()
    // You may want to remove the following line if you have errors downloading dependencies.
    mavenLocal()
    maven { url = 'https://central.sonatype.com/repository/maven-snapshots/' }
    maven { url = 'https://jitpack.io' }
  }
}

eclipse.project.name = 'FirstDemo' + '-parent'
